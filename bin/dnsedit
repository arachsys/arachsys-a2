#!/bin/perl -w

use Arachsys::A2 qw(checkdomain checkdomainuser checkprivilege);
use File::Basename;
use File::Compare;
use File::Copy;
use File::Temp;
use Getopt::Std;
use IPC::Open2;
use Term::ReadKey;
use strict;

sub confirm() {
  ReadMode 'cbreak';
  my $key = ReadKey(0);
  ReadMode 'normal';
  print STDERR "$key\n";
  return lc $key eq 'y' ? 1 : 0;
}

sub usage() {
  print STDERR <<EOF;
Usage:  $0 DOMAIN  edit DNS data below DOMAIN
        $0 -a      edit all DNS data (privileged user only)
EOF
  exit 1;
}

sub HELP_MESSAGE { usage; }
sub VERSION_MESSAGE { usage; }

$SIG{__DIE__} = sub { die "$0: $_[0]"; };
$SIG{__WARN__} = sub { warn "$0: $_[0]"; };
$SIG{INT} = $SIG{TERM} = sub { exit 1; };

my $editor = $ENV{VISUAL} || $ENV{EDITOR} || "vi";
my $privileged = checkprivilege;
$0 = basename $0;
umask 0077;

my %option = ();
getopts "a", \%option or usage;

my $matchdomain;
if (not $option{a} and @ARGV == 1) {
  $matchdomain = lc $ARGV[0];
  unless (checkdomain $matchdomain) {
    die "Domain $matchdomain is invalid\n";
  }
  unless ($privileged or checkdomainuser $matchdomain) {
    die "Domain $matchdomain does not belong to you\n";
  }
} elsif (not $option{a} or @ARGV != 0) {
  usage;
} elsif (not $privileged) {
  die "-a mode is only available to privileged users\n";
} else {
  $matchdomain = "-a";
}

my $editfile = new File::Temp;
my $origfile = new File::Temp;

system "/bin/dnsconfig -r $matchdomain >$origfile";
die "Failed to read DNS records for editing\n" unless $? == 0;
copy $origfile->filename, $editfile->filename
  or die "Failed to create file for editing\n";

while (1) {
  system $editor, $editfile;
  die "Failed to edit DNS records\n" unless $? == 0;
  unless (compare $origfile->filename, $editfile->filename) {
    warn "DNS records unmodified\n";
    exit;
  }

  my $checkfile = new File::Temp;
  my $pid = open2 \*OUT, \*IN, "/bin/dnsconfig -rw $matchdomain";
  copy \*OUT, $checkfile->filename;
  close \*OUT;

  if (compare $origfile->filename, $checkfile->filename) {
    copy $checkfile->filename, \*IN;
    close \*IN;
    waitpid $pid, 0;
    warn "DNS records have changed while you were editing\n";
    print STDERR "Save anyway? (y/n) ";
    exit 1 unless confirm;
    $origfile = $checkfile;
    system "/bin/dnsconfig -w $matchdomain <$editfile";
  } else {
    copy $editfile->filename, \*IN;
    close \*IN;
    waitpid $pid, 0;
  }

  if ($? == 0) {
    warn "DNS records successfully updated\n";
    exit;
  }

  warn "Failed to update DNS records\n";
  print STDERR "Return to editor? (y/n) ";
  exit 1 unless confirm;
}
