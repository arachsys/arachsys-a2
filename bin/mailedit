#!/bin/perl -w

use Arachsys::A2 qw(checkdomain checkip checkdomainuser checkipuser);
use File::Basename;
use File::Compare;
use File::Copy;
use File::Temp;
use Getopt::Std;
use IPC::Open2;
use Term::ReadKey;
use strict;

my $editor = $ENV{VISUAL} || $ENV{EDITOR} || "vi";
my $privileged = $< == 0;

sub confirm() {
  ReadMode 'cbreak';
  my $key = ReadKey(0);
  ReadMode 'normal';
  print STDERR "$key\n";
  return lc $key eq 'y' ? 1 : 0;
}

sub usage() {
  print STDERR <<EOF;
Usage:  $0 DOMAIN  edit addresses for DOMAIN
        $0 -a      edit all addresses (privileged user only)
        $0 -p IP   edit SMTP AUTH credentials for IP
EOF
  exit 1;
}

sub HELP_MESSAGE { usage; }
sub VERSION_MESSAGE { usage; }

$SIG{__DIE__} = sub { die "$0: $_[0]"; };
$SIG{__WARN__} = sub { warn "$0: $_[0]"; };
$SIG{INT} = $SIG{TERM} = sub { exit 1; };

$0 = basename $0;
umask 0077;

my %option = ();
getopts "ap", \%option or usage;

my $matchdomain;
if ($option{p}) {
  usage unless not $option{a} and @ARGV == 1;
  my $passwdip;
  unless ($passwdip = checkip $ARGV[0]) {
    die "IP address $ARGV[0] is invalid\n";
  }
  unless ($privileged or checkipuser $passwdip) {
    die "IP address $passwdip does not belong to you\n";
  }
  $matchdomain = "-p $passwdip";
} elsif (not $option{a} and @ARGV == 1) {
  $matchdomain = $ARGV[0];
  unless ($matchdomain = checkdomain $matchdomain) {
    die "Domain $matchdomain is invalid\n";
  }
  unless ($privileged or checkdomainuser $matchdomain) {
    die "Domain $matchdomain does not belong to you\n";
  }
} elsif ($option{a} and @ARGV == 0 and $privileged) {
  $matchdomain = "-a";
} elsif ($option{a} and @ARGV == 0) {
  die "-a mode is only available to privileged users\n";
} else {
  usage;
}

my $editfile = new File::Temp;
my $origfile = new File::Temp;

system "/bin/mailconfig -r $matchdomain >$origfile";
die "Failed to read mail configuration for editing\n" unless $? == 0;
copy $origfile->filename, $editfile->filename
  or die "Failed to create file for editing\n";

while (1) {
  system $editor, $editfile;
  die "Failed to edit mail configuration\n" unless $? == 0;
  unless (compare $origfile->filename, $editfile->filename) {
    warn "DNS records unmodified\n";
    exit;
  }

  my $checkfile = new File::Temp;
  my $pid = open2 \*OUT, \*IN, "/bin/mailconfig -rw $matchdomain";
  copy \*OUT, $checkfile->filename;
  close \*OUT;

  if (compare $origfile->filename, $checkfile->filename) {
    copy $checkfile->filename, \*IN;
    close \*IN;
    waitpid $pid, 0;
    warn "Mail configuration has changed while you were editing\n";
    print STDERR "Save anyway? (y/n) ";
    exit 1 unless confirm;
    $origfile = $checkfile;
    system "/bin/mailconfig -w $matchdomain <$editfile";
  } else {
    copy $editfile->filename, \*IN;
    close \*IN;
    waitpid $pid, 0;
  }

  if ($? == 0) {
    warn "Mail configuration successfully updated\n";
    exit;
  }

  warn "Failed to update mail configuration\n";
  print STDERR "Return to editor? (y/n) ";
  exit 1 unless confirm;
}
