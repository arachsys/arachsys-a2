#!/bin/perl -w

use Arachsys::A2 qw(checkip checkipuser);
use File::Basename;
use IPC::Open2;
use strict;

my $userpattern = qr/[a-z0-9-=_@][a-z0-9-=_.@]*/;
my $privileged = $< == 0;

sub usage() {
  print STDERR "Usage: $0 IP USERNAME\n";
  exit 1;
}

$SIG{__DIE__} = sub { die "$0: $_[0]"; };
$SIG{__WARN__} = sub { warn "$0: $_[0]"; };
$SIG{INT} = $SIG{TERM} = sub { exit 1; };

$0 = basename $0;
usage unless @ARGV == 2;

my $passwdip;
die "IP address $ARGV[0] is invalid\n"
  unless $passwdip = checkip $ARGV[0];
die "IP address $passwdip does not belong to you\n"
  unless $privileged or checkipuser $passwdip;

my $username = $ARGV[1];
die "Invalid username '$username'\n"
  unless $username =~ /^$userpattern$/;

my $password = `dovecotpw -s PLAIN` or exit 1;
chomp $password; $password =~ s/^{[^}]*}//;

my $pid = open2 \*OUT, \*IN, "/bin/mailconfig -rwp $passwdip";
while (<OUT>) {
  if ((split /[:\n]/)[0] eq $username) {
    print IN "$username:$password\n";
  } else {
    print IN $_;
  }
}
close OUT;
close IN;
